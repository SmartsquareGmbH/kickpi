apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'


apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.smartsquare.kickpi"
        minSdkVersion 27
        targetSdkVersion 28
        versionCode 3
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            buildConfigField "String", "KICKWAY_URL", "\"${getFromSecrets("KICKWAY_URL")}\""
            buildConfigField "String", "LEFT_GOAL_GPIO", "\"${getFromSecrets("LEFT_GOAL_GPIO")}\""
            buildConfigField "String", "RIGHT_GOAL_GPIO", "\"${getFromSecrets("RIGHT_GOAL_GPIO")}\""
            buildConfigField "String", "SCORE_TO_FINISH_GAME", "\"${getFromSecrets("SCORE_TO_FINISH_GAME")}\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "KICKWAY_URL", "\"${getFromSecrets("KICKWAY_URL")}\""
            buildConfigField "String", "LEFT_GOAL_GPIO", "\"${getFromSecrets("LEFT_GOAL_GPIO")}\""
            buildConfigField "String", "RIGHT_GOAL_GPIO", "\"${getFromSecrets("RIGHT_GOAL_GPIO")}\""
            buildConfigField "String", "SCORE_TO_FINISH_GAME", "\"${getFromSecrets("SCORE_TO_FINISH_GAME")}\""
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // io
    compileOnly "com.google.android.things:androidthings:$androidThingsVersion"
    implementation "com.google.android.gms:play-services-nearby:$nearbyVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$gsonConverterVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.github.SmartsquareGmbh:kickprotocol:$kickprotocolVersion"

    // dependency inejction
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"
    implementation "org.koin:koin-android-scope:$koinVersion"
    //noinspection GradleDependency - justification: incompatible with version 2
    implementation "com.github.rubengees:kotterknife:$kotterknifeVersion"

    // android
    implementation "com.mikepenz:iconics-core:$iconicsVersion"
    implementation "com.mikepenz:iconics-views:$iconicsVersion"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support.constraint:$constraintLayoutVersion"
    implementation "com.android.support:support-v4:$androidSupportVersion"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.github.kirich1409:StrictModeCompat:$strictModeCompatVersion"
    implementation "com.uber.autodispose:autodispose-ktx:$autodisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-ktx:$autodisposeVersion"
    implementation "com.uber.autodispose:autodispose-android-ktx:$autodisposeVersion"
    implementation "com.mikepenz:community-material-typeface:$materialTypefaceVersion"
    implementation "android.arch.lifecycle:extensions:$androidLifecycleExtensionVersion"

    // firebase
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"


    // testing
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.amshove.kluent:kluent-android:$kluentVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

detekt {
    version = detektVersion

    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/config/detekt/detekt.yml"
        output = "$buildDir/reports/detekt"
    }
}

ktlint {
    version = ktlintVersion
    reporters = ["CHECKSTYLE"]
}

gradle.projectsEvaluated {
    check.dependsOn detektCheck
}

static String getFromSecrets(String key) {
    if (isCI()) {
        return 'dummy'
    }

    Properties result = new Properties()
    try {
        result.load(new FileInputStream(new File("secrets.properties")))
    } catch (Exception ignored) {
        throw new GradleException("Please add a secrets.properties file with a value for $key to perform this action.")
    }

    if (!result.containsKey(key)) {
        throw new GradleException("Please include a value for $key in your secrets.properties " +
                "file to perform this action.")
    }

    return result[key]
}

static boolean isCI() {
    Object isCI = System.getenv('CI')

    return isCI != null && isCI.toBoolean()
}
